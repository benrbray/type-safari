@top Program { expr }

@precedence {
  highprec,
  exp @left,
  type @right
}

@skip { space | LineComment }

/* ---- general ----------------------------------------- */

kw<term>   { @specialize[@name={term}]<lowerIdent, term> }
sym<term>  { @specialize[@name={term}]<symbols, term> }
_sym<term> { @specialize<symbols, term> }

parens<rule> { "(" rule ")" }

arrow { sym<"->"> }

/* ---- types ------------------------------------------- */

forall {
  kw<"forall"> |
  @specialize[@name="forall"]<symbols, "∀">
}

ForallDot { sym<"."> }

TypeVar { lowerIdent }
MetaVar { _sym<"?"> ident }
TypeCon { upperIdent }

type {
  TypeVar |
  MetaVar |
  TypeCon |
  TypeArr |
  parens<type> |
  forall TypeVar+ ForallDot type
}

TypeArr { type !type arrow type }

/* --- terms -------------------------------------------- */

TermVar { lowerIdent }

// expression with optional type annotation
expr {
  expr0 |
  TypeAnn { expr0 (!highprec "::" type) }
}

// expression, without type annotation
expr0 {
  LetExpr { kw<"let"> Decl kw<"in"> expr } |
  IfThenElse { kw<"if"> expr kw<"then"> expr kw<"else"> expr } |
  Abs { lambda TermVar LambdaArrow expr } |
  BinaryExpr |
  UnaryExpr |
  TermVar |
  literal |
  AppExpr |
  parens<expr>
}

Decl {
  TermVar sym<"="> expr
}

/* ---- lambda ---- */

lambda      { @specialize[@name=Lambda]<symbols, "\\" | "λ"> }
LambdaArrow { arrow }

/* ---- application ---- */

AppExpr { (TermVar|parens<expr>) (TermVar|parens<expr>|AppExpr) }

/* ---- operators ---- */

plusOp { @specialize[@name=ArithOp]<symbols, "+"|"-"> }
multOp { @specialize[@name=ArithOp]<symbols, "*"|"/"> }
cmpOp  { @specialize[@name=CmpOp]<symbols, ">"|">="|"<"|"<="> }
binOp  { plusOp | multOp | cmpOp }

BinaryExpr { expr !exp binOp expr }

UnaryExpr { plusOp expr }

/* ---- literals ---- */

literal {
  @specialize[@name=BooleanLiteral]<upperIdent, "True" | "False"> |
  IntLiteral |
  StringLiteral
}

/* ---- tokens ------------------------------------------ */

@tokens {
  /* characters */
  space { $[ \t\n\r]+ }
  digit { std.digit }
  
  /* named identifiers */
  underline {"_"}
  small { std.asciiLowercase | underline}
  large { std.asciiUppercase } 
  identchar { (small | large | digit) }
  ident      { identchar+  }
  lowerIdent { small ident }
  upperIdent { large ident }
  
  /* symbols */
  symbol { "+" | "-" | "*" | "/" |
           "<" | ">" | "=" |
           "#" | "?" | "@" |
           "\\" | "λ" | "∀" |
           "." | "," | "?" }
  symbols { (symbol)+ }
  
  /* literals */
  IntLiteral { (digit)+ }
  StringLiteral { '"' (!["\\] | "\\" _)* '"' }
  
  /* comments */
  LineComment { "--" ("-")* ![\n]* }

  "(" ")" // TODO is it necessary to keep these on same line?

  @precedence { LineComment, symbols } // resolve conflict between -- and -
}

@detectDelim